//https://www.youtube.com/watch?v=Ouza_4SsbLc
//https://codepen.io/rickyjw/pen/qrjmdz
//https://editor.p5js.org/Coulomb1/sketches/ajYsnHRW2
//https://www.coursera.org/lecture/nand2tetris2/unit-3-10-graphics-optimization-hHNmi
//https://classes.engineering.wustl.edu/cse365/demo_program.php
//https://arieljannai.gitlab.io/Nand2TetrisBitmapEditor/
//https://github.com/ErikUmble/nand2tetris/tree/main/DinoAdventure

//https://www.redblobgames.com/pathfinding/a-star/introduction.html
//https://www.youtube.com/watch?v=5DhCoqoPmzs

//https://github.com/IshankGulati/Image-Processing-Mini-projects/blob/master/Astar%20Search/pathPlanning.py



class Game
{
    field int Direction;
    field Character Character;
    field Finish Finish;
    field int MoveAreaWidth;
    field int ScreenMiddle;
    field int LeftBorder;
    field int RightBorder;

    field int PlatformsCount;
    field int ObstaclesCount;
    field int EnemiesCount;
    field int CollectablesCount;
    field int BulletsCount;

    field Array Platforms;
    field Array Obstacles;
    field Array Enemies;
    field Array Collectables;
    field Array Bullets;

    field Level1 Level;

    field int GameDelay;

    field int redrawAllObstaclesTimer;
    field int redrawAllObstaclesDelay;

    field Array map;
    field int mapWidth;
    field int mapHeight;

    field int WorldOffset;

    static int currLevel;
    static String text;

    static int Kills;

    
    
    function void init()
    {
        let Kills = 0;
        let currLevel = 1;
        return;
    }

    constructor Game new()
    {
        let MoveAreaWidth = 3; // 5
        let ScreenMiddle = 16;
        let LeftBorder = ScreenMiddle - MoveAreaWidth;
        let RightBorder = ScreenMiddle + MoveAreaWidth;

        let GameDelay = 20; //20

        let redrawAllObstaclesDelay = 7;
        let redrawAllObstaclesTimer = redrawAllObstaclesDelay;

        return this;
    }

    method void drawHeart()
    {
        var int memAddress;
        let memAddress = Constants.getScreenStart() + 8;

	    do Memory.poke(memAddress + 96, 3696);
	    do Memory.poke(memAddress + 128, 8184);
	    do Memory.poke(memAddress + 160, 8184);
	    do Memory.poke(memAddress + 192, 4080);
	    do Memory.poke(memAddress + 224, 2016);
	    do Memory.poke(memAddress + 256, 960);
	    do Memory.poke(memAddress + 288, 384);

        return;
    }

    method void drawCoin()
    {
        var int memAddress;
        let memAddress = Constants.getScreenStart() + 12;

        do Memory.poke(memAddress + 32, 2016);
	    do Memory.poke(memAddress + 64, 2064);
	    do Memory.poke(memAddress + 96, 5064);
	    do Memory.poke(memAddress + 128, 5160);
	    do Memory.poke(memAddress + 160, 5160);
	    do Memory.poke(memAddress + 192, 5160);
	    do Memory.poke(memAddress + 224, 5160);
	    do Memory.poke(memAddress + 256, 5064);
	    do Memory.poke(memAddress + 288, 2064);
	    do Memory.poke(memAddress + 320, 2016);

        return;
    }

    method void drawAmmo()
    {
        var int memAddress;
        let memAddress = Constants.getScreenStart() + 16;

        do Memory.poke(memAddress + 32, 12684);
	    do Memory.poke(memAddress + 64, 12684);
	    do Memory.poke(memAddress + 96, 31710);
	    do Memory.poke(memAddress + 128, 31710);
	    do Memory.poke(memAddress + 160, 19026);
	    do Memory.poke(memAddress + 192, 19026);
	    do Memory.poke(memAddress + 224, 19026);
	    do Memory.poke(memAddress + 256, 19026);
	    do Memory.poke(memAddress + 288, 19026);
	    do Memory.poke(memAddress + 320, 12684);
        return;
    }

    method void drawSkull()
    {
        var int memAddress;
        let memAddress = Constants.getScreenStart() + 28;

        //do Memory.poke(memAddress + 32, 1016);
	    //do Memory.poke(memAddress + 64, 1548);
	    //do Memory.poke(memAddress + 96, 3078);
	    //do Memory.poke(memAddress + 128, 2050);
	    //do Memory.poke(memAddress + 160, 2446);
	    //do Memory.poke(memAddress + 192, 2478);
	    //do Memory.poke(memAddress + 224, 3074);
	    //do Memory.poke(memAddress + 256, 3590);
	    //do Memory.poke(memAddress + 288, 1620);
	    //do Memory.poke(memAddress + 320, 1020);

        do Memory.poke(memAddress + 32, 4064);
	    do Memory.poke(memAddress + 64, 8176);
	    do Memory.poke(memAddress + 96, 8176);
	    do Memory.poke(memAddress + 128, 7088);
	    do Memory.poke(memAddress + 160, 12568);
	    do Memory.poke(memAddress + 192, 13208);
	    do Memory.poke(memAddress + 224, 16376);
	    do Memory.poke(memAddress + 256, 7920);
	    do Memory.poke(memAddress + 288, 8176);
	    do Memory.poke(memAddress + 320, 1984);
	    do Memory.poke(memAddress + 352, 1344);

        return;
    }


    method void printLevel()
    {
        do Output.moveCursor(0, 1);
        let text = "Level ";
        do Output.printString(text);
        do Output.printInt(currLevel);
        
        if (Difficulty.getDifficulty() = 1)
        {
            let text = " Easy";
        }
        else { if (Difficulty.getDifficulty() = 2)
        {
            let text = " Medium";
        }
        else { if (Difficulty.getDifficulty() = 3)
        {
            let text = " Hard";
        }
        else { if (Difficulty.getDifficulty() = 4)
        {
            let text = " Insane";
        }}}}

        do Output.printString(text);

        return;
    }


    function void addKill() 
    { 
        let Kills = Kills + 1; 
        do Game.printKills();
        return;
    }
    function void printKills()
    {   
        do Output.moveCursor(0, 58);
        let text = ": ";
        do Output.printString(text);
        do Output.printInt(Kills);

        return;
    }

    
    method void getLevel()
    {
        if (currLevel = 1)
        {
            let Level = Level1.new();
        }
        else { if (currLevel = 2)
        {
            let Level = Level2.new();
        }
        else {if (currLevel = 3)
        {
            let Level = Level3.new();
        }}}

        return;
    }

    method void setupGame()
    {
        //do Screen.drawLine(0, 15, 511, 15);
        do Screen.drawLine(125, 0, 125, 15);

        //// CHOOSE LEVEL
        //let currLevel = 1;
        //// CHOOSE DIFFICULTY
        do Difficulty.setDifficulty(2);

        do getLevel();

        let map = Level.getMap();
        let mapWidth = Level.getMapWidth();
        let mapHeight = Level.getMapHeight();
        do Map.setWidth(mapWidth);
        do Map.setHeight(mapHeight);
        do Map.setMap(map);

        let Character = Level.getCharacter();

        do printLevel();
        do drawHeart();
        do drawCoin();
        do drawAmmo();
        do drawSkull();
        do Character.printHealth();
        do Character.printCoins();
        do Character.printAmmo();
        do Game.printKills();

        let Finish = Level.getFinish();

        let PlatformsCount = Level.getPlatformsCount();
        let ObstaclesCount = Level.getObstaclesCount();
        let EnemiesCount = Level.getEnemiesCount();
        let CollectablesCount = Level.getCollectablesCount();

        let Platforms = Level.getPlatforms();
        let Obstacles = Level.getObstacles();
        let Enemies = Level.getEnemies();
        let Collectables = Level.getCollectables();

        do GetBulletsCount();
        if (~(BulletsCount = 0))
        {
            let Bullets = Array.new(BulletsCount);
            do GetBullets();
        }

        let WorldOffset = 0;

        do DrawAllPlatforms();
        do DrawAllCollectables();

        return;
    }

    method void start()
    {
        var char key;
        var bool exit;
        var int i;
        var int j;
        var int temp;
        var Platform platform;
        var int value;

        let exit = false;

        while (~exit)
        {
            let key = Keyboard.keyPressed();

            if (key = 0)
            {
                let Direction = 0;
            }
            else { if (key = 131)
            {
                do Character.Jump();
            }
            else { if (key = 81) 
            {
                let exit = true;
            }
            else { if (key = 130) 
            {
                let Direction = 1;
            }
            else { if (key = 132) 
            {
                let Direction = 2;
            }
            else { if (key = 32)
            {
                do Character.Shoot(WorldOffset);
            }}}}}}
            

            if (Character.IsCollidedWithFinish(Finish))
            {
                let exit = true;
                
                //do Output.printString("finish");

                if (currLevel < 3)
                {
                    do Screen.clearScreen();
                    let currLevel = currLevel + 1;
                    do Level.dispose();
                    do setupGame();
                    do start();
                }
            }

            
            do Move();
            do Sys.wait(GameDelay);
            


            do Character.RedrawPlatforms(WorldOffset);

            do DoAllEnemyActions();

            do CheckEnemyAndCharacterCollision();

            do RefreshBullets();
            if (~(BulletsCount = 0))
            {
                do CheckAllBullets();
                do CheckAllBulletCollisions();
            }
            
            do CheckCollectableAndCharacterCollision();

            do CheckTimers();

            do DecrementTimers();
        }

        return;
    }

    

    // GAME TIMERS
    method void CheckTimers()
    {
        if (redrawAllObstaclesTimer < 1)
        {
            do RedrawAllObstacles();
            let redrawAllObstaclesTimer = redrawAllObstaclesDelay;
        }

        do Character.CheckTimers();
        do CheckAllEnemyTimers();
        do CheckAllCollectableTimers();
        
        

        return;
    }

    
    method void DecrementTimers()
    {
        let redrawAllObstaclesTimer = redrawAllObstaclesTimer - 1;

        do Character.DecrementTimers();
        do DecrementAllEnemyTimers();
        do DecrementAllCollectableTimers();

        

        return;
    }

    // ENEMY TIMERS
    method void CheckAllEnemyTimers()
    {
        var Enemy enemy;
        var int i;

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            do enemy.CheckTimers();
            let i = i + 1;
        }

        return;
    }

    method void DecrementAllEnemyTimers()
    {
        var Enemy enemy;
        var int i;

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            do enemy.DecrementTimers();
            let i = i + 1;
        }

        return;
    }

    //// COLLECRTABLE TIMERS
    method void CheckAllCollectableTimers()
    {
        var Collectable collectable;
        var int i;

        do Heart.CheckTimers();
        do Coin.CheckTimers();

        do DrawAllCollectables();

        return;
    }

    method void DecrementAllCollectableTimers()
    {
        var Collectable collectable;
        var int i;

        do Heart.DecrementTimers();
        do Coin.DecrementTimers();

        return;
    }

    method void DrawAllPlatforms()
    {
        var Platform platform;
        var int i;

        while (i < PlatformsCount)
        {
            let platform = Platforms[i];
            do platform.Draw();
            let i = i + 1;
        }

        return;
    }

    method void DrawAllCollectables()
    {
        var int i;
        var Collectable collectable;

        while (i < CollectablesCount)
        {
            let collectable = Collectables[i];
            do collectable.Draw(true);
            let i = i + 1;
        }

        return;
    }

    method void RedrawAllObstacles()
    {
        var Obstacle obstacle;
        var int i;

        if (Character.getShouldRedrawObstacles())
        {
            while (i < ObstaclesCount)
            {
                let obstacle = Obstacles[i];
                do obstacle.Draw();

                let i = i + 1;
            }
            do Character.setShouldRedrawObstacles(false);
        }

        return;
    }

    method void DoAllEnemyActions()
    {
        var Enemy enemy;
        var int i;

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            //do enemy.DoActions(map, mapWidth, mapHeight, WorldOffset, Character.get_x(), Character.get_y());
            do Character.DoEnemyAction(enemy, map, mapWidth, mapHeight, WorldOffset);
            let i = i + 1;
        }

        return;
    }

    method void CheckEnemyAndCharacterCollision()
    {
        var int i;
        var Enemy enemy;
        var bool collided_sides;
        var bool collided_top;

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];

            do Character.CheckCollisionWithEnemy(enemy);

            let i = i + 1;
        }

        return;
    }


    method void CheckAllBullets()
    {
        var int i;
        var Enemy enemy;

        do Character.CheckBullets(WorldOffset);

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            do enemy.CheckBullets(WorldOffset);
            let i = i + 1;
        }

        return;
    }

    method void GetBulletsCount()
    {
        var int i;
        var int count;
        var Enemy enemy;

        let count = Character.getBulletsCount();
        

        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            
            let count = count + enemy.getBulletsCount();
            let i = i + 1;
        }

        let BulletsCount = count;

        return;
    }

    method void RefreshBullets()
    {
        if (Character.getShoudlRefreshGameBullets())
        {
            if (~(BulletsCount = 0))
            {
                do Bullets.dispose();
            }

            do GetBulletsCount();
            
            if (~(BulletsCount = 0))
            {
                do GetBullets();
            }
            do Character.setShoudlRefreshGameBullets(false);
        }

        return;
    }

    method void GetBullets()
    {
        var int i;
        var int j;
        var Array bullets;
        var Enemy enemy;
        var int arrSize;

        let Bullets = Array.new(BulletsCount);

        let bullets = Character.getBullets();

        while (i < Character.getBulletsCount())
        {
            let Bullets[i] = bullets[i];
            let i = i + 1;
        }

        let arrSize = Character.getBulletsCount();

        let i = 0;
        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            let bullets = enemy.getBullets();

            let j = 0;
            while (j < enemy.getBulletsCount())
            {
                let Bullets[arrSize + j] = bullets[j];

                let j = j + 1;
            }

            let arrSize = arrSize + enemy.getBulletsCount();

            let i = i + 1;
        }

        return;
    }

    method void CheckAllBulletCollisions()
    {
        var int i;
        var int j;
        var Enemy enemy;
        var Bullet bullet;

        while (i < BulletsCount)
        {
            let bullet = Bullets[i];

            do Character.CheckCollisionWithBullet(bullet);

            let j = 0;
            while (j < EnemiesCount)
            {
                let enemy = Enemies[j];

                do enemy.CheckCollisionWithBullet(bullet);

                let j = j + 1;
            }

            let i = i + 1;
        }       

        return;
    }

    method void CheckCollectableAndCharacterCollision()
    {
        var int i;
        var Collectable collectable;

        while (i < CollectablesCount)
        {
            let collectable = Collectables[i];

            do Character.CheckCollisionWithCollectable(collectable);

            let i = i + 1;
        }

        return;
    }




    method void ScrollLeft()
    {
        var int i;
        var Platform platform;
        var Obstacle obstacle;
        var Enemy enemy;
        var Collectable collectable;

        

        let WorldOffset = WorldOffset + 1;
        do Character.incrementAbsoluteX(1);

        let i = 0;
        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            do enemy.ScrollLeft();

            let i = i + 1;
        }

        let i = 0;
        while (i < CollectablesCount)
        {
            let collectable = Collectables[i];
            do collectable.ScrollLeft();

            let i = i + 1;
        }

        let i = 0;
        while (i < ObstaclesCount)
        {
            let obstacle = Obstacles[i];
            do obstacle.ClearScrollLeft();

            let i = i + 1;
        }

        let i = 0;
        while (i < PlatformsCount)
        {
            let platform = Platforms[i];
            do platform.ClearScrollLeft();

            let i = i + 1;
        }

        let i = 0;
        while (i < ObstaclesCount)
        {
            let obstacle = Obstacles[i];
            do obstacle.DrawScrollLeft();

            let i = i + 1;
        }

        let i = 0;
        while (i < PlatformsCount)
        {
            let platform = Platforms[i];
            do platform.DrawScrollLeft();

            let i = i + 1;
        }

        do Finish.ScrollLeft();


        return;
    }

    method void ScrollRight()
    {
        var int i;
        var Platform platform;
        var Obstacle obstacle;
        var Enemy enemy;
        var Collectable collectable;

        let WorldOffset = WorldOffset - 1;
        do Character.incrementAbsoluteX(-1);

        let i = 0;
        while (i < EnemiesCount)
        {
            let enemy = Enemies[i];
            do enemy.ScrollRight();

            let i = i + 1;
        }

        let i = 0;
        while (i < CollectablesCount)
        {
            let collectable = Collectables[i];
            do collectable.ScrollRight();

            let i = i + 1;
        }

        let i = 0;
        while (i < ObstaclesCount)
        {
            let obstacle = Obstacles[i];
            do obstacle.ClearScrollRight();

            let i = i + 1;
        }

        let i = 0;
        while (i < PlatformsCount)
        {
            let platform = Platforms[i];
            do platform.ClearScrollRight();

            let i = i + 1;
        }

        let i = 0;
        while (i < ObstaclesCount)
        {
            let obstacle = Obstacles[i];
            do obstacle.DrawScrollRight();

            let i = i + 1;
        }

        let i = 0;
        while (i < PlatformsCount)
        {
            let platform = Platforms[i];
            do platform.DrawScrollRight();

            let i = i + 1;
        }

        do Finish.ScrollRight();

        return;
    }

    method void Move()
    {
        var int x;
        var int absX;
        var int cWidth;
        
        let x = Character.getX();
        let absX = Character.getAbsoluteX();
        let cWidth = Character.getWidth();

        do Character.CheckGravity(WorldOffset);
        do Character.CheckCollisionWithObstacles();

        if (Direction = 1) 
        {
            do Character.setMovingLeft(true);

            if (~Character.CheckCollisionWithPlatformLeft(WorldOffset))
            {
                if (absX < LeftBorder)
                {
                    do Character.MoveLeft();
                }
                else { if (x < LeftBorder)
                {
                    do ScrollRight();
                }
                else 
                {
                    do Character.MoveLeft();
                }}
            }
        }
        if (Direction = 2)
        {
            do Character.setMovingLeft(false);

            if (~Character.CheckCollisionWithPlatformRight(WorldOffset))
            {
                if (absX > (mapWidth - RightBorder))
                {
                    do Character.MoveRight();
                }
                else { if (x > (RightBorder - cWidth))
                {
                    do ScrollLeft();
                }
                else 
                {
                    do Character.MoveRight();
                }}
            }
        }
        
        return;
    }
}
    