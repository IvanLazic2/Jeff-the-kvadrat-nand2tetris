class Collision
{

    //// PLATFORMS
    static int lastY;
    static int platformY;

    function int getLastY() { return lastY; }
    function int getPlatformY() { return platformY; }

    function int getNearestPlatformY(int col, int y, int height)
    {
        var int currIndex;
        var int nearestPlatformY;

        let nearestPlatformY = Map.getHeight() * 16;

        let currIndex = (y / 16) + 1;

        while (currIndex < Map.getHeight())
        {
            if ((-1 < (y + 16)) & ((y + 16) < 256))
            {
                if (Collision.IsPlatform(Map.getValue(col, currIndex)))
                { 
                    let nearestPlatformY = currIndex * 16;
                    let currIndex = Map.getHeight();
                }
            }
            
            let currIndex = currIndex + 1;
        }

        return nearestPlatformY;
    }

    function bool IsOnAnyPlatform(int x, int y, int width, int height, int forceY, int worldOffset)
    {
        var bool is_stable_x;
        var bool is_stable_y;
        var int platform_x;
        var int h;
        var int w;
        var int nearestPlatformY;
        var int i;
        var int minNearestPlatformY;
        var int py;

        let minNearestPlatformY = (Map.getHeight()) * 16;
        while (i < width)
        {
            let py = Collision.getNearestPlatformY(x + i + worldOffset, y, height);
            if ((py > 0) & (py < minNearestPlatformY))
            {
                let minNearestPlatformY = py;
            }

            let i = i + 1;
        }

        let platformY = minNearestPlatformY;

        let h = minNearestPlatformY - y - Constants.getBlockHeight();
        let is_stable_y = (-1 < h) & (h < (forceY + 1));

        if (Math.abs(y + Constants.getBlockHeight() + forceY - minNearestPlatformY) < 7) // < 15
        {
            let is_stable_y = true;
        }

        if (minNearestPlatformY > ((Map.getHeight()) * 16))
        {
            let is_stable_y = false;
        }

        
        return is_stable_y;
    }

    function bool CheckAnyPlatformCollisionRight(int x, int y, int width, int height, int worldOffset)
    {
        var int i;
        var bool result;
        var int matrixValue;

        if (~((-1 < y) & (y < 256)))
        {
            return false;
        }

        let i = ((y + 8)/16);
        while (i > ((y / 16) - height))
        {
            let matrixValue = Map.getValue(x + width + worldOffset, i);

            if ((5 < matrixValue) & (matrixValue < 11))
            {
                return false;
            }

            let result = result | Collision.IsPlatform(matrixValue);

            let i = i - 1;
        }

        return result;
    }

    function bool CheckAnyPlatformCollisionLeft(int x, int y, int width, int height, int worldOffset)
    {
        var int i;
        var bool result;
        var int matrixValue;

        if (~((-1 < y) & (y < 256)))
        {
            return false;
        }

        let i = ((y + 8)/16);
        while (i > ((y / 16) - height))
        {
            let matrixValue = Map.getValue(x - 1 + worldOffset, i);

            if ((5 < matrixValue) & (matrixValue < 11))
            {
                return false;
            }
            
            let result = result | Collision.IsPlatform(matrixValue);
            let i = i - 1;
        }

        return result;
    }

    function bool IsPlatform(int value)
    {
        return ((0 < value) & (value < 11));
    }   

    function bool IsObstacle(int value)
    {
        return ((10 < value) & (value < 16));
    }

    function bool IsCollectable(int value)
    {
        return ((15 < value) & (value < 19));
    }

    function bool GetPlatformType(int value)
    {
        return value - 1;
    }

    function bool IsAtPlatformBoundaryLeft(int x, int y, int width, int height, int worldOffset)
    {
        return ~Collision.IsPlatform(Map.getValue(x - 1 + worldOffset, (y / 16) + 1));
    }

    function bool IsAtPlatformBoundaryRight(int x, int y, int width, int height, int worldOffset)
    {
        return ~Collision.IsPlatform(Map.getValue(x + width + worldOffset, (y / 16) + 1));
    }

    //// OBSTACLES
    function bool IsCollidedAnyObstacle(int x, int y, int width, int height)
    {
        var bool result;
        var int i;
        var int j;
        var int blockValue;

        if (~((-1 < (y - (height * 16))) & ((y - (height * 16)) < 256)))
        {
            return false;
        }

        while (i < width)
        {
            let j = (y / 16) - (height - 1);

            while (j < ((y / 16) + 1))
            {
                let result = result | (Collision.IsObstacle(Map.getValue(x + i, j)));

                let j = j + 1;
            }

            if (result)
            {
                let i = width;
            }

            let i = i + 1;
        }
        
        return result;
    }

    function void RedrawPlatforms(int x, int y, int width, int height, int worldOffset)
    {
        var int i;
        var int j;
        var int px;
        var int py;
        var int memAddress;

        if (((-1 < (x - 1)) & ((x - 1) < 32)) & ((-1 < (x + width + 1)) & ((x + width + 1) < 32)))
        {
            let i = x - 1;
            while (i < (x + width + 1))
            {
                let j = ((y - height) / 16) - 2;
                while (j < (((y + Constants.getBlockHeight()) / 16) + 2))
                {
                    if (((i + worldOffset) < Map.getWidth()) & (j < Map.getHeight()))
                    {
                        if (Collision.IsPlatform(Map.getValue(i + worldOffset, j)))
                        {
                            let memAddress = Constants.getMemAddress(i, j * 16);

                            do Platform.DrawBlock(Map.getValue(i + worldOffset, j) - 1, memAddress, i, j);
                        }
                    }
                    
                    let j = j + 1;
                }

                let i = i + 1;
            }
        }
        

        return;
    }

    function bool IsEntityCollidedWithEntitySides(int cx, int cy, int cw, int ch, int ex, int ey, int ew, int eh)
    {
        // TEMP FIX!!!
        if (ch = 2)
        {
            let ch = 32;
        }

        return (ex < (cx + cw)) & (cx < (ex + ew)) & ((ey) > (cy - ch)) & ((ey) < (cy + 16));
    }

    function bool IsEntityCollidedWithEntityTop(int cx, int cy, int cw, int ch, int ex, int ey, int ew, int eh)
    {
        return (ex < (cx + cw)) & (cx < (ex + ew)) & ((ey - eh - 24) < (cy)) & ((cy) < (ey - eh));
    }

    function bool IsBulletCollidedWithMap(int x, int y, bool drawLeft, int worldOffset)
    {
        var bool result;
        var int col;

        if (((-1 < x) & (x < 32)) & ((-1 < y) & (y < 256)))
        {
            if (drawLeft)
            {
                let col = x - 1 + worldOffset;
            }
            else 
            {
                let col = x + 1 + worldOffset;
            }

            let result = (Map.getValue(col, y / 16) = 0) & (Map.getValue(col, (y + 15)/16) = 0);
        }

        return ~result;
    }

    function bool IsBulletCollidedWithEntity(int ex, int ey, int ew, int eh, int bx, int by)
    {
        var bool colx;
        var bool coly;

        let colx = (bx < (ex + ew)) & ((bx + 1) > ex);
        let coly = ((by + 15) > (ey - eh + 15)) & (by < (ey + 15));

        return colx & coly;
    }

    function bool HasPlatformAbove(int x, int y, int width, int worldOffset, bool movingLeft)
    {
        var int i;
        var int j;
        var int limit;
        var bool result;

        if (movingLeft)
        {
            let i = x - 3; // 1
            let limit = x;
        }
        else
        {
            let i = x + width - 1;
            let limit = x + width + 3; //x + width
        }

        while (i < limit)
        {
            let j = 0;
            while (j < 3)
            {
                if ((y / 16) - j > -1)
                {
                    let result = result | (Collision.IsPlatform(Map.getValue(i + worldOffset, (y / 16) - j)));
                }
                
                let j = j + 1;
            }

            let i = i + 1;
        }
        
        return result;
    }

    function bool HasObstacleBelow(int x, int y, int width, int worldOffset, bool movingLeft)
    {
        var int i;
        var int j;
        var int limit;
        var bool result;

        if (movingLeft)
        {
            let i = x;
            let limit = x + 1;
        }
        else
        {
            let i = x + width - 1;
            let limit = x + width;
        }

        while (i < limit)
        {
            let j = y / 16;
            while (j < 16)
            {
                let result = result | (Collision.IsObstacle(Map.getValue(i + worldOffset, j)));
                let j = j + 1;
            }

            let i = i + 1;
        }

        return result;
    }

    function int GetObstacleBelowY(int x, int y, int width, int worldOffset, bool movingLeft)
    {
        var int i;
        var int j;
        var int limit;
        var int result;

        if (movingLeft)
        {
            let i = x;
            let limit = x + 1;
        }
        else
        {
            let i = x + width - 1;
            let limit = x + width;
        }

        let result = 256;
        while (i < limit)
        {
            let j = y;
            while (j < (Map.getHeight() * 16))
            {
                if (Collision.IsObstacle(Map.getValue(i + worldOffset, j / 16)))
                {
                    if (j < result)
                    {
                        let result = j;
                    }
                }
                let j = j + 16;
            }
            let i = i + 1;
        }

        

        return result / 16;
    }

    function bool GetPlatformBelowY(int x, int y, int width, int worldOffset, bool movingLeft)
    {
        var int i;
        var int j;
        var int limit;
        var int result;

        if (movingLeft)
        {
            let i = x - 3;
            let limit = x + 1;
        }
        else
        {
            let i = x + width - 1;
            let limit = x + width + 3; // + 2 s movement delayom 3
        }

        let result = 256;
        while (i < limit)
        {
            let j = y; // 
            while (j < (Map.getHeight() * 16))
            {
                if (Collision.IsPlatform(Map.getValue(i + worldOffset, j / 16)))
                {
                    if (j < result)
                    {
                        let result = j;
                    }
                }
                let j = j + 16;
            }

            let i = i + 1;
        }

        return result / 16;
    }
    
}